buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.7.1'
        classpath 'org.javamodularity:moduleplugin:1.8.12'
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'com.google.osdetector' version '1.7.1'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
}

ext.platform = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

application {
    mainModule = 'eu.hansolo.fx.glucostatus'
    mainClass  = 'eu.hansolo.fx.glucostatus.Launcher'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation "org.openjfx:javafx-base:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-graphics:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-controls:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-media:${javafxVersion}:${platform}"
    implementation "org.openjfx:javafx-swing:${javafxVersion}:${platform}"
    implementation 'eu.hansolo:jdktools:17.0.27'
    implementation 'eu.hansolo:toolbox:17.0.33'
    implementation 'eu.hansolo:toolboxfx:17.0.33'
    implementation 'eu.hansolo:applefx:17.0.45'
    implementation 'com.google.code.gson:gson:2.9.0'
    //implementation 'com.dustinredmond.fxtrayicon:FXTrayIcon:4.0.1'
    implementation 'com.dustinredmond.fxtrayicon:FXTrayIcon:3.1.2'
}

ext.moduleName = 'eu.hansolo.fx.glucostatus'

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes(
                'Implementation-Title': 'GlucoStatusFX',
                'Class-Path': 'glucostatusfx-17.0.17.jar',
                'Main-Class': 'eu.hansolo.fx.glucostatus.Launcher'
        )
    }
}


/*
tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}
*/

// start app from gradle
task Main(type: JavaExec) {
    mainClass = "eu.hansolo.fx.glucostatus.Launcher"
    classpath = sourceSets.main.runtimeClasspath
}


// create properties file including the version
task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir//classes/java/main/eu/hansolo/fx/glucostatus/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}


// Fix problems with loading resources
sourceSets {
    main {
        //output.setResourcesDir(java.outputDir)
        output.resourcesDir = file('build/classes/java/main')
        java.destinationDirectory.set(file('build/classes/java/main'))
    }
}

processResources {
    from(sourceSets.main.java.srcDirs) {
        include '**/*.properties'
    }
}

run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}