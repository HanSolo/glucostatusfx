buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
        maven {
            url "https://sandec.jfrog.io/artifactory/repo"
        }
    }
    dependencies {
        classpath 'com.google.gradle:osdetector-gradle-plugin:1.7.0'
        classpath 'com.sandec.jpro:jpro-plugin-gradle:2021.2.2'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.google.osdetector' version '1.7.0'
    id 'org.openjfx.javafxplugin' version '0.0.12'
}

apply plugin: 'com.sandec.jpro'

ext.platform = osdetector.os == 'osx' ? osdetector.arch == 'aarch_64' ? 'mac-aarch64' : 'mac' : osdetector.os == 'windows' ? 'win' : osdetector.os == 'linux' ? osdetector.arch == 'aarch_64' ? 'linux-aarch64' : 'linux' : osdetector.os

normalization {
    runtimeClasspath {
        ignore('/META-INF/MANIFEST.MF')
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    implementation 'eu.hansolo:jdktools:17.0.3'
    implementation 'eu.hansolo:toolbox:17.0.12'
    implementation 'eu.hansolo:toolboxfx:17.0.22'
    implementation 'eu.hansolo:applefx:17.0.21'
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'com.dustinredmond.fxtrayicon:FXTrayIcon:3.1.2'
    implementation "com.sandec.jpro:jpro-webapi:2021.2.2"
}

javafx {
    version       = '17.0.2'
    modules       = [ 'javafx.base', 'javafx.graphics', 'javafx.controls', 'javafx.media', 'javafx.swing']
    configuration = 'compileOnly'
}

ext.moduleName = 'eu.hansolo.fx.glucostatus'
mainClassName = 'eu.hansolo.fx.glucostatus.Main'

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.runtimeClasspath.collect {  it.isDirectory() ? it : zipTree(it)  }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes(
                'Implementation-Title': 'GlucoStatusFX',
                'Class-Path': 'glucostatusfx-17.0.1.jar',
                'Main-Class': 'eu.hansolo.fx.glucostatus.Main'
        )
    }
}

// Fix problems with loading resources
sourceSets {
    main {
        //output.setResourcesDir(java.outputDir)
        output.resourcesDir = file('build/classes/java/main')
        java.destinationDirectory.set(file('build/classes/java/main'))
    }
}

processResources {
    from(sourceSets.main.java.srcDirs) {
        include '**/*.properties'
    }
}


run {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--module', mainClassName
        ]
        classpath = files()
    }
}


jpro {
    // for debugging
    // JVMArgs << '-agentlib:jdwp=transport=dt_socket,server=n,address=5006,suspend=y'

    JVMArgs << '-Xmx1000m'

    //jpro server port
    port = 8080

}